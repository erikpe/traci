#ifndef TECHNIC_PLATE_WITH_HOLES_TRACI
#define TECHNIC_PLATE_WITH_HOLES_TRACI

#include "../primitives/lego-units.traci"
#include "../primitives/lego-primitives.traci"

def legopieceTechnicPlateWithHoles (length)
{
    boxLength = length*LU;
    boxHeight = .4*LU;
    boxWidth = 2*LU;
    pegHeight = .2*LU;
    return union {
        difference {
            roundedBox (boxLength, boxHeight, boxWidth, LEGO_ROUNDING);
            for (x in 0 .. length-2) {
                legopartTechnicPlateHole () {
                    translate X*(x*LU);
                }
            }
        }
        for (x in 0 .. length-1) {
            legopartPeg () {
                translate [x*LU, boxHeight, 0];
            }
            legopartPeg() {
                translate [x*LU, boxHeight, LU];
            }
        }
        bbox {
            scale [boxLength, boxHeight+pegHeight, boxWidth];
        }
    }
}

#endif // TECHNIC_PLATE_WITH_HOLES_TRACI
