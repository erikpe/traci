#ifndef LEGO_TECHNIC_BRICK_WITH_HOLES_TRACI_
#define LEGO_TECHNIC_BRICK_WITH_HOLES_TRACI_

#include "common/basic-units.traci"
#include "common/basic-shapes.traci"
#include "lego-units.traci"
#include "lego-primitives.traci"

def legoTechnicBrickWithHoles_fast (length)
{
    boxLength = LU * length;
    boxHeight = LU * 1.2;
    boxWidth = LU;
    pegHeight = LU * .2;
    shrink = LU*.01;
    return union {
        difference {
            box {
                scale [boxLength-2*shrink, boxHeight-2*shrink, boxWidth-2*shrink];
                translate [1, 1, 1] * shrink;
            }
            for (i in 0 .. length-2) {
                fastLegopartTechnicBrickHole() {
                    translate X*i*LU;
                }
            }
        }
        for (i in 0 .. length-1) {
            legopartPeg () {
                translate [LU*i, boxHeight, 0];
            }
        }
        bbox {
            scale [boxLength, boxHeight+pegHeight, boxWidth];
        }
    }
}

def legoTechnicBrickWithHoles_accurate (length)
{
    boxLength = LU * length;
    boxHeight = LU * 1.2;
    boxWidth = LU;
    thickness = LU * .15;
    pegHeight = LU * .2;
    return union {
        difference {
            roundedBox1 (boxLength, boxHeight, boxWidth, LEGO_ROUNDING);
            difference {
                roundedBox2 (boxLength-2*thickness, boxHeight-thickness, boxWidth-2*thickness, LEGO_ROUNDING) {
                    translate [thickness, 0, thickness];
                }
                for (i in 0 .. length-2) {
                    cylinder (LU*.3+thickness*.5, [LU, LU*.7, thickness-EPSILON], [LU, LU*.7, boxWidth-thickness+EPSILON]) {
                        translate X*LU*i;
                    }
                }
            }
            for (i in 0 .. length-2) {
                legopartTechnicBrickHole () {
                    translate X*i*LU;
                }
            }
        }
        for (i in 0 .. length-2) {
            roundedCylinder0 (LU*.15, LU*.35, LEGO_ROUNDING) {
                scale [1, -1, 1];
                translate [LU, LU*.35, LU*.5];
                translate X*LU*i;
            }
        }
        for (i in 0 .. length-1) {
            legopartPegHole () {
                translate [LU*i, boxHeight, 0];
            }
        }
        bbox {
            scale [boxLength, boxHeight+pegHeight, boxWidth];
        }
    }
}

/*
 * http://www.bricklink.com/catalogList.asp?catType=P&catString=135
 */
def legoTechnicBrickWithHoles (length)
{
#if FAST_LEGO
    return legoTechnicBrickWithHoles_fast (length);
#else
    return legoTechnicBrickWithHoles_accurate (length);
#endif // FAST_LEGO
}

/*
 * Technic, Brick 1 x 2 with Hole
 * http://www.bricklink.com/catalogItem.asp?P=3700
 */
def legoTechnicBrickWithHoles_1x2 ()
{
    return legoTechnicBrickWithHoles (2);
}

/*
 * Technic, Brick 1 x 4 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=3701
 */
def legoTechnicBrickWithHoles_1x4 ()
{
    return legoTechnicBrickWithHoles (4);
}

/*
 * Technic, Brick 1 x 6 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=3894
 */
def legoTechnicBrickWithHoles_1x6 ()
{
    return legoTechnicBrickWithHoles (6);
}

/*
 * Technic, Brick 1 x 8 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=3702
 */
def legoTechnicBrickWithHoles_1x8 ()
{
    return legoTechnicBrickWithHoles (8);
}

/*
 * Technic, Brick 1 x 10 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=2730
 */
def legoTechnicBrickWithHoles_1x10 ()
{
    return legoTechnicBrickWithHoles (10);
}

/*
 * Technic, Brick 1 x 12 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=3895
 */
def legoTechnicBrickWithHoles_1x12 ()
{
    return legoTechnicBrickWithHoles (12);
}

/*
 * Technic, Brick 1 x 14 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=32018
 */
def legoTechnicBrickWithHoles_1x14 ()
{
    return legoTechnicBrickWithHoles (14);
}

/*
 * Technic, Brick 1 x 16 with Holes
 * http://www.bricklink.com/catalogItem.asp?P=3703
 */
def legoTechnicBrickWithHoles_1x16 ()
{
    return legoTechnicBrickWithHoles (16);
}

#endif // LEGO_TECHNIC_BRICK_WITH_HOLES_
